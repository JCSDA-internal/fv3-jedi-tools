! -----------------------------------------------------------------------------
! Ferret script to plot FV3 fields stored in GFS format
! Source: https://extranet.gfdl.noaa.gov/~atw/ferret/cubed_sphere/
! Modified by Benjamin Menetrier for FV3-JEDI
! -----------------------------------------------------------------------------

! Input arguments

! File name (without the tile number and the extension)
def sym filename $1

! Figure title
def sym title $2

! View longitude
def sym lonview $3

! View latitude
def sym latview $4

! Color map
def sym colormap $5

! Centered levels
def sym centered $6

! Output file name
def sym output $7

! -----------------------------------------------------------------------------

! Read NetCDF file generated by raster.py
can dat/all
let minlev=1e38
let maxlev=-1e38
rep/name=s/range=1:6 (\
   use ($filename)`s`.nc;\
   let var`s`=var[d=`var,r=dsetnum`];\
   let mintest=var`s`[x=@min,y=@min,z=@min,t=@min];\
   if `mintest lt minlev` then let minlev=`mintest` endif;\
   let maxtest=var`s`[x=@max,y=@max,z=@max,t=@max];\
   if `maxtest gt maxlev` then let maxlev=`maxtest` endif;\
   let lon`s`=lon[d=`lon,r=dsetnum`];\
   let lat`s`=lat[d=`lat,r=dsetnum`];\
   )

! Centered color map
if `strcmp("($centered)","True") eq 0` then
   let maxlev=max(abs(`minlev`),abs(`maxlev`))
   let minlev=-`maxlev`
endif

! Define windows
set window/aspect=1.05/size=.3 1
ppl cross 0\
ppl color 6 100 100 100\
ppl shaset reset
palette ($colormap)

! Define levels
let nlev=30
let delta=(maxlev-minlev)/nlev

! Plot cube-sphere data
go plot_cubesphere_ortho "shade/nolab/lev=(`minlev`,`maxlev`,`delta`)" var lon lat ($lonview) ($latview)

! Define a global grid for land overlays
def ax/x=0:360:360 pcso_xdum
def ax/y=-90:90:180 pcso_ydum
def grid/x=pcso_xdum/y=pcso_ydum pcso_gdum
set grid pcso_gdum
go mp_land 7 " " " " " " " " " " 0

! Set title
set text/font=arial
annotate/xpos=0/ypos=1.05/halign=0/valign=0/size=0.15 "($title)"

! Write file
frame/file=($output).png/xpixels=3000
